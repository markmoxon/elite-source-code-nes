;REM >EliteIsp5
;	H%=L%+P%-C%
;	O%=W%
;	W=5
;	W2=16
;	WY=12
;	W2Y=2.5*WY
;	D=80

;[OPTZ

HIMCNT	BRK
ZINF2	LDA	#0
	LDX	#NI%-1
DML1	STA	INWK,X
	DEX
	BPL	DML1
	LDA	#96
	STA	INWK+18
	STA	INWK+22
	STA	INWK+14
	RTS
TWIST2	EQUB	&2C
TWIST	LDA	#0
	STA	RAT2
	LDX	#15
	LDY	#9
	JSR	MVS5
	LDX	#17
	LDY	#11
	JSR	MVS5
	LDX	#19
	LDY	#13
	JMP	MVS5
STORE	LDY	#NI%-1
DML2	LDA	INWK,Y
	STA	(INF),Y
	DEY
	BPL	DML2
	RTS
DEMON
	LDA	#1
	JSR	TT66
	JSR	RESET
	LDA	#0
	STA	ALPHA
	STA	ALP1
	STA	DELTA
	STA	scacol+CYL
	JSR	DOVDU19
	JSR	nWq
	LDX	#(acorn MOD256)
	LDY	#(acorn DIV256)
	JSR	SLIDE
	JSR	ZINF2
	LDA	#128
	STA	INWK+5
	LDA	#100
	STA	INWK+3
	LDA	#LGO
	STA	TYPE
	JSR	NWSHP
	LDA	#150
	STA	MCNT
FLYL1	LDA	INWK+6
	CLC
	ADC	#3
	STA	INWK+6
	LDA	INWK+7
	ADC	#0
	STA	INWK+7
	JSR	LL9
	DEC	MCNT
	BNE	FLYL1
FLYL2	LDA	INWK+6
	CLC
	ADC	#2
	STA	INWK+6
	LDA	INWK+7
	ADC	#0
	STA	INWK+7
	LDA	#128
	JSR	TWIST2
	\DECINWK+3
	JSR	LL9
	LDA	INWK+14
	BPL	FLYL2
	JSR	STORE
	JSR	ZINF2
	LDA	#108
	STA	INWK+3
	LDA	#40
	STA	INWK+6
	LDA	#128
	STA	INWK+8
	LDA	#CYL
	STA	TYPE
	JSR	NWSHP
	LDA	#1
	STA	INWK+27
	STA	HIMCNT
	LDA	#90
	STA	MCNT
	JSR	TWIST
	JSR	TWIST
	JSR	TWIST
FLYL4	JSR	LL9
	JSR	MVEIT
	DEC	MCNT
	BNE	FLYL4
	DEC	HIMCNT
	BPL	FLYL4
	JSR	ZZAAP
	LDA	#0
	JSR	NOISE
	LDY	#10
	JSR	DELAY
	LDA	#44
	STA	INWK+29
	LDA	#8
	STA	INWK+27
	LDA	#&87
	STA	INWK+30
	JSR	STORE
	LDA	#128
	TSB	K%+31 \++
	JSR	EXNO3
	JSR	ZZAAP
FLYL5	LDX	#NI%-1
DML3	LDA	K%,X
	STA	INWK,X
	DEX
	BPL	DML3
	INX
	JSR	GINF
	LDA	XX21-2+2*LGO
	STA	XX0
	LDA	XX21-1+2*LGO
	STA	XX0+1
	LDA	#LGO
	STA	TYPE
	INC	INWK
	JSR	LL9
	JSR	STORE
	JSR	PBFL
	LDA	INWK+31
	AND	#&A0
	CMP	#&A0
	PHP
	LDX	#NI%-1
DML4	LDA	K%+NI%,X
	STA	INWK,X
	DEX
	BPL	DML4
	LDX	#1
	JSR	GINF
	LDA	XX21-2+2*CYL
	STA	XX0
	LDA	XX21-1+2*CYL
	STA	XX0+1
	LDA	#CYL
	STA	TYPE
	JSR	MVEIT
	JSR	LL9
	JSR	STORE
	PLP
	BNE	FLYL5
	LDA	#14
	STA	DELTA
	STZ	DELT4 \++
	LSR	A
	ROR	DELT4
	LSR	A
	ROR	DELT4
	STA	DELT4+1
	LDX	#(exec MOD256)
	LDY	#(exec DIV256)
	JSR	SLIDE
	LDX	#(byian MOD256)
	LDY	#(byian DIV256)
	JSR	SLIDE
	LDX	#NI%-1
DML5	LDA	K%+NI%,X
	STA	INWK,X
	DEX
	BPL	DML5
FLYL6	JSR	STARS1
	JSR	MVEIT
	JSR	LL9
	LDA	INWK+8
	BPL	FLYL6
	LDX	#(true3 MOD256)
	LDY	#(true3 DIV256)
	JSR	SLIDE
	JSR	RES2
	LDA	#&E0
	STA	INWK+14
	STZ	DELTA
	STZ	ALPHA
	STZ	ALP1 \++
	LDX	#15
	STX	INWK+27
	LDX	#5
	STX	INWK+7
	LDA	#ADA
	STA	TYPE
	JSR	NWSHP
FLYL7	JSR	MVEIT
	JSR	LL9
	LDA	INWK+7
	BNE	FLYL7
	LDA	#3
	STA	INWK+29
	STA	INWK+30
	LDA	INWK+8
	BPL	FLYL7
	JSR	SCAN
	JSR	RES2
	LDA	#CYAN2
	STA	scacol+CYL
	JMP	DEATH2 \More Demo stuff here
SLIDE	JSR	GRIDSET
	JSR	ZEVB
	LDA	#YELLOW
	JSR	DOCOL
	LDA	#254
	STA	BALI
SLL2	JSR	GRID
	DEC	BALI
	DEC	BALI
	BNE	SLL2
SL1	JSR	ZEVB
	LDA	#2
	STA	BALI
GRID	LDY#0
	STY	UPO
	STY	INWK+8
	STY	INWK+1
	STY	INWK+4
	DEY
GRIDL	INY
	STZ	INWK+7 \++
	LDA	Y1TB,Y
	BNE	*+5
	JMP	GREX
	SEC
	SBC	BALI
	BCC	GRIDL
	STA	R
	ASL	A
	ROL	INWK+7
	ASL	A
	ROL	INWK+7
	ADC	#D
	STA	INWK+6
	LDA	INWK+7
	ADC	#0
	STA	INWK+7
	STZ	S
	LDA	#128
	STA	P
	JSR	ADD
	STA	INWK+5
	STX	INWK+3
	LDA	X1TB,Y
	EOR	#128
	BPL	GR2
	EOR	#FF
	INA	\++
GR2	STA	INWK
	LDA	X1TB,Y
	EOR	#128
	AND	#128
	STA	INWK+2
	STY	YS
	JSR	PROJ
	LDY	YS
	LDA	K3
	STA	XX15
	LDA	K3+1
	STA	XX15+1
	LDA	K4
	STA	XX15+2
	LDA	K4+1
	STA	XX15+3
	STZ	INWK+7 \++
	LDA	Y2TB,Y
	SEC
	SBC	BALI
	BCC	GR6
	STA	R
	ASL	A
	ROL	INWK+7
	ASL	A
	ROL	INWK+7
	ADC	#D
	STA	INWK+6
	LDA	INWK+7
	ADC	#0
	STA	INWK+7
	STZ	S
	LDA	#128
	STA	P
	JSR	ADD
	STA	INWK+5
	STX	INWK+3
	LDA	X2TB,Y
	EOR	#128
	BPL	GR3
	EOR	#FF
	INA	\++
GR3	STA	INWK
	LDA	X2TB,Y
	EOR	#128
	AND	#128
	STA	INWK+2
	JSR	PROJ
	LDA	K3
	STA	XX15+4
	LDA	K3+1
	STA	XX15+5
	LDA	K4
	STA	XX12
	LDA	K4+1
	STA	XX12+1
	JSR	LL145
	LDY	YS
	BCS	GR6
	INC	UPO
	LDX	UPO
	LDA	X1
	STA	X1UB,X
	LDA	Y1
	STA	Y1UB,X
	LDA	X2
	STA	X2UB,X
	LDA	Y2
	STA	Y2UB,X
GR6	JMP	GRIDL
GREX
	LDY	UPO
	BEQ	GREX2
GRL2	LDA	Y1VB,Y
	BEQ	GR4
	STA	Y1
	LDA	X1VB,Y
	STA	X1
	LDA	X2VB,Y
	STA	X2
	LDA	Y2VB,Y
	STA	Y2
	JSR	LOIN
GR4	LDA	X1UB,Y
	STA	X1
	STA	X1VB,Y
	LDA	Y1UB,Y
	STA	Y1
	STA	Y1VB,Y
	LDA	X2UB,Y
	STA	X2
	STA	X2VB,Y
	LDA	Y2UB,Y
	STA	Y2
	STA	Y2VB,Y
	JSR	LOIN
	DEY
	BNE	GRL2
	JSR	LBFL
GREX2	RTS
ZEVB	LDA	#0
	TAY
SLL1	STA	Y1VB,Y
	DEY
	BNE	SLL1
	RTS
GRIDSET	STX	GSL1+1
	STY	GSL1+2
	LDA	#254
	STA	YP
	LDY	#0
	LDX	#0
	STX	XP
GSL1	LDA	P%,Y
	BEQ	GRSEX
	STY	T
	SEC
	SBC	#44
	STA	S
	ASL	A
	ASL	A
	ADC	S
	TAY
	LDA	LTDEF,Y
	JSR	GRS1
	LDA	LTDEF+1,Y
	JSR	GRS1
	LDA	LTDEF+2,Y
	JSR	GRS1
	LDA	LTDEF+3,Y
	JSR	GRS1
	LDA	LTDEF+4,Y
	JSR	GRS1
	LDY	T
	INY
	LDA	XP
	CLC
	ADC	#W2
	STA	XP
	BCC	GSL1
	LDA	#0
	STA	XP
	LDA	YP
	SBC	#W2Y
	STA	YP
	JMP	GSL1
GRSEX	LDA	#0
	STA	Y1TB,X
	RTS
GRS1	BEQGRR1
	STA	R
	AND	#15
	STY	P
	TAY
	LDA	NOFX,Y
	CLC
	ADC	XP
	STA	X1TB,X
	LDA	YP
	SEC
	SBC	NOFY,Y
	STA	Y1TB,X
	LDA	R
	LSR	A
	LSR	A
	LSR	A
	LSR	A
	TAY
	LDA	NOFX,Y
	CLC
	ADC	XP
	STA	X2TB,X
	LDA	YP
	SEC
	SBC	NOFY,Y
	STA	Y2TB,X
	INX
	LDY	P
GRR1	RTS
ZZAAP	LDA	#RED
	JSR	DOCOL
	LDA	#128
	STA	X1
	STA	X2
	LDA	#67
	STA	Y1
	LDA	#160
	STA	Y2
	JMP	LL30
LTDEF	DB	63,34,47,76,97
	DB	35,00,00,00,00
	DB	63,34,47,76,00
	DB	61,00,00,00,00
	DB	73,31,15,57,00
	DB	31,17,00,00,00
	DB	02,25,53,36,68
	DB	02,28,86,35,00
	DB	82,23,35,00,00
	DB	20,03,35,58,86
	DB	20,06,68,85,53
	DB	02,28,00,00,00
	DB	60,02,28,86,35
	DB	82,20,03,35,00
	DB	00,00,00,00,00
	DB	00,00,00,00,00
	DB	00,00,00,00,00
	DB	00,00,00,00,00
	DB	00,00,00,00,00
	DB	00,00,00,00,00
	DB	00,00,00,00,00
	DB	60,02,28,35,00
	DB	60,02,28,86,35
	DB	86,60,02,00,00
	DB	60,05,56,00,00
	DB	86,60,02,35,00
	DB	60,02,35,00,00
	DB	45,58,86,60,02
	DB	60,28,35,00,00
	DB	17,00,00,00,00
	DB	28,86,63,00,00
	DB	60,23,83,00,00
	DB	86,60,00,00,00
	DB	60,04,42,28,00
	DB	60,08,82,00,00
	DB	60,02,28,86,00
	DB	60,02,25,53,00
	DB	60,02,28,86,48
	DB	60,02,25,53,48
	DB	20,03,35,58,86
	DB	02,17,00,00,00
	DB	28,86,60,00,00
	DB	27,70,00,00,00
	DB	28,84,46,60,00
	DB	26,08,00,00,00
	DB	74,04,24,00,00
	DB	02,26,68,00,00

NOFX	EQUB	4
	EQUB	8
	EQUB	12
	EQUB	4
	EQUB	8
	EQUB	12
	EQUB	4
	EQUB	8
	EQUB	12
	EQUB	4
	EQUB	8
	EQUB	12
NOFY	EQUB	0
	EQUB	0
	EQUB	0
	EQUB	WY
	EQUB	WY
	EQUB	WY
	EQUB	2*WY
	EQUB	2*WY
	EQUB	2*WY
	EQUB	5*WY/2
	EQUB	5*WY/2
	EQUB	5*WY/2


;\           \......+......./\......+......./\......+......./\......+......"
acorn DB	":::ACORNSOFT::::;;;;;;;;;;;;;;;;::::PRESENTS":EQUB0
byian DB	"::::::BY:;::::::;;;;IAN;BELL;;;;::::::AND:::::::;;DAVID;BRABEN":EQUB0
exec  DB	"::::::THE;::::::;;;EXECUTIVE;;;;::::VERSION":EQUB0
true3 DB	"CONGRATULATIONS:FOR:SHOWING:RARETASTE:IN:OPTING:FOR:THIS:VERSION":EQUB0


